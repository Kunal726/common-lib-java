plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'com.projects.marketmosaic'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

repositories {
    mavenCentral()
    maven { url = uri("https://repo.spring.io/milestone") }
}

ext {
    set('springCloudVersion', "2024.0.0")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'       // Web application support
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'  // Health checks and metrics
    implementation 'org.springframework.boot:spring-boot-starter-data-redis' // Redis integration
    implementation 'org.springframework.boot:spring-boot-autoconfigure' //spring auto configure

    // ===== Spring Cloud ZooKeeper =====
    implementation 'org.springframework.cloud:spring-cloud-starter-zookeeper-config' // ZooKeeper config
    implementation 'org.springframework.cloud:spring-cloud-starter-zookeeper-discovery' // ZooKeeper discovery

    // ===== Monitoring and Metrics =====
    api 'io.micrometer:micrometer-registry-prometheus:1.11.5'    // Prometheus metrics integration

    // ===== API Documentation =====
    api 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0' // OpenAPI/Swagger documentation

    // ===== Utilities =====
    api 'org.apache.commons:commons-lang3:3.14.0'                // Apache Commons utilities
    api 'com.fasterxml.jackson.core:jackson-databind:2.16.1'     // JSON processing
    implementation 'org.apache.commons:commons-pool2:2.12.0'                // Object pooling

    // ===== Database =====
    runtimeOnly 'com.mysql:mysql-connector-j'                              // MySQL database driver

    // ===== Development Tools =====
    compileOnly 'org.projectlombok:lombok'                                 // Code generation
    annotationProcessor 'org.projectlombok:lombok'                         // Lombok annotation processor


    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'org.springframework.boot:spring-boot-starter-log4j2' //logging
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java  // Publish the Java component (e.g., JAR)
        }
    }
    repositories {
        maven {
            name = "CustomLocalRepo"
            url = uri("file:///Users/kunal.kulthe/Downloads/MyProjects/MarketMosaic/common-repo") // Custom local path
        }
    }
}
